#+TITLE: Emacs Configurations
#+AUTHOR: Zhitao Gong
#+EMAIL: me@gongzhitaao.org
#+HTML_HEAD: <meta name="description" content="Emacs configurations written in Orgmode.">
#+HTML_HEAD: <meta name="keywords" content="emacs,orgmode,literal programming,emacs-lisp,org">
#+HTML_HEAD: <link rel="stylesheet" href="normalize.css" type="text/css" />
#+HTML_HEAD: <link rel="stylesheet" href="https://gongzhitaao.github.io/orgcss/org.css" type="text/css" />
#+OPTIONS: H:4 num:3 toc:nil num:t
#+TAGS: export(e) noexport(n)
#+STARTUP: fold content
#+MACRO: kbd @@html:<kbd>$1</kbd>@@

-----

#+BEGIN_ABSTRACT
However I organize my Emacs configurations, they all seem a mess to me
after a while.
#+END_ABSTRACT

#+TOC: headlines 2

* Introduction
  :PROPERTIES:
:CUSTOM_ID: sec:introduction
:END:

  This is my Emacs configuration, stored in =conf.org= file with all
  these texts and loaded by =org-babel-load-file= from =init.el=.  This
  is inspired by [[http://writequit.org/][Matthew Lee Hinman]][fn:1] in his [[http://writequit.org/org/settings.html][settings]].  This project
  is hosted with â™¥ by Github, here is the [[https://github.com/gongzhitaao/dotemacs][repo]].

  This file is last updated {{{time(%Y-%m-%d %H:%M)}}}.

* Bootstrap
  :PROPERTIES:
:CUSTOM_ID: sec:bootstrap
:END:

  I use =emacs.d/init.el= to bootstrap all my Emacs configurations.  The
  only thing done in =init.el= is to load this file, =conf.org=, which
  includes all our documented configurations.

  #+BEGIN_SRC emacs-lisp :tangle no
(require 'org)
(org-babel-load-file "~/.emacs.d/conf.org")
  #+END_SRC

  The following sections are all the documented configurations.

* Package Manager
  :PROPERTIES:
:CUSTOM_ID: sec:pacman
:END:

  Before any loading and configuration, I need to make sure all my
  utility packages are installed.

** /Deprecated/ Manager

   This is how I did, and probably what you are doing.

   1. First of all, maintain a list of packages.

      #+BEGIN_SRC emacs-lisp :tangle no
(defvar my/package-list
  '(

    ;; A super long list of all packages

    ))
      #+END_SRC

   2. Then add official archives and initialize the package system.

      #+BEGIN_SRC emacs-lisp :tangle no
(require 'package)
(setq package-archives
      '(("gnu" . "http://elpa.gnu.org/packages/")
        ("marmalade" . "http://marmalade-repo.org/packages/")
        ("melpa" . "http://melpa.org/packages/")
        ("org" . "http://orgmode.org/elpa/")))
(package-initialize)

(or (file-exists-p package-user-dir) (package-refresh-contents))
      #+END_SRC

   3. And finally install, if neccessary all the packages.

      #+BEGIN_SRC emacs-lisp :tangle no
(dolist (pack my/package-list)
  (unless (package-installed-p pack)
    (package-install pack)))
      #+END_SRC


   It works.  But there is one thing I particularly do not like about
   the default packaging system: when updating packages, it
   /re-downloads/ all the outdated package instead of retrieving only
   modified files like =git pull=.  It takes quite a while to uncompress
   and compile large packages, e.g., =ess= (Emacs Speaks Statistics),
   =org=, =org-plus-contrib= and etc.  And Emacs is blocked during this
   process since it is /single-threaded/!

** Preferred Way

   Just found about [[http://cask.readthedocs.org/en/latest/index.html][Cask]], yet another package manager for Emacs.  See
   [[https://cask.readthedocs.org/en/latest/guide/introduction.html][Why Cask?]] for the reason of switching to Cask.  All dependencies are
   stored in =Cask= under =.emacs.d/=.

   The contents of Cask is as follows (long list of packages are omitted
   for brevity).

   #+BEGIN_SRC emacs-lisp :tangle no
(source gnu)
(source melpa)
(source marmalade)
(source org)

(depends-on "ace-jump-mode")
(depends-on "ag")
(depends-on "anzu")
(depends-on "async")
(depends-on "auctex")
;; the (depends-on ...) list goes on and on
   #+END_SRC

   And the following snippet is needed to initialize the =cask= manager.

   #+BEGIN_SRC emacs-lisp
(require 'cask "~/.cask/cask.el")
(cask-initialize)
   #+END_SRC

   For more information about Cask, please refer to its
   [[http://cask.readthedocs.org/en/latest/][documentation]].

   And as usual, load =use-package= first which is used for loading
   packages everywhere else.

   #+BEGIN_SRC emacs-lisp
(require 'use-package)
   #+END_SRC

   Disable debugging information for package loading.

   #+BEGIN_SRC emacs-lisp
(setq use-package-verbose nil)
   #+END_SRC

* Helper Variables and Functions
  :PROPERTIES:
:CUSTOM_ID: sec:helper
:END:

  Now start with some useful variables and function.

  #+BEGIN_SRC emacs-lisp
(setq user-full-name "Zhitao Gong")
(setq user-mail-address "me@gongzhitaao.org")

(defvar my-dir user-emacs-directory
  "The root dir for my Emacs configuration.")
(defvar my-tmp (expand-file-name "tmp" my-dir)
  "Temp files that might be useful but that I don't care about.")
(defvar my-personal-dir (expand-file-name "~/Dropbox/emacs/personal"))
(defvar my-icons-dir (expand-file-name "icons" my-dir)
  "Where All the icons are stored.")

(unless (file-exists-p my-tmp) (make-directory my-tmp))
  #+END_SRC

  Then follows some helper functions.

  #+BEGIN_SRC emacs-lisp
(defun my-apply-region-or-line (func)
  "Apply FUNC to a region, or current line if mark is not
      active."
  (save-excursion
    (if (region-active-p)
        (funcall func (region-beginning) (region-end))
      (funcall func (line-beginning-position) (line-end-position)))))

(defun my-apply-region-or-para (func)
  "Apply FUNC to a region, or current paragraph if mark is not active."
  (save-excursion
    (if (not (region-active-p))
        (mark-paragraph))
    (funcall func (region-beginning) (region-end))))

(defun sort-words (reverse beg end)
  "Sort words in region alphabetically, in REVERSE if
      negative. Prefixed with negative \\[universal-argument], sorts
      in reverse.

        The variable `sort-fold-case' determines whether alphabetic
        case affects the sort order.

        See `sort-regexp-fields'."
  (interactive "*P\nr")
  (sort-regexp-fields reverse "\\(\\w\\|-\\)+" "\\&" beg end))

(defmacro rename-modeline (package-name mode new-name)
  "Rename modeline."
  `(eval-after-load ,package-name
     '(defadvice ,mode (after rename-modeline activate)
        (setq mode-name ,new-name))))
  #+END_SRC

* Packages and Modes
  :PROPERTIES:
:CUSTOM_ID: sec:packmode
:END:

  This section contains goodies that enpower Emacs.  Two of the most
  important packages I'm using, Orgmode and Gnus are kept in separate
  configuration files and listed in separeted sections, while all others
  are all listed under Section [[#sec:miscpac]].

** Gnus
   :PROPERTIES:
:CUSTOM_ID: sec:gnus
:END:

   #+BEGIN_QUOTE
   Gnus is a flexible message reader running under GNU Emacs.  It
   supports reading and composing both news and mail.  In addition, it
   is able to use a number of web-based sources as inputs for its
   groups.
   #+END_QUOTE

   I choose Gnus over other GUI or console email client mainly because
   it is a builtin Emacs package, which gives me access to most of the
   powerful goodies when composing and reading emails.

   With all that being said, it has a rather steep learning curve, three
   years for me.  However, as a heavy Emacs user, it worth the effort.

*** Tool chain

    Gnus can be used to handle all the process of mail, i.e., fetching
    mails from server as well as local mail pool, reading mails and
    performing various operations on mails and send mails.  However, I
    prefer to deligate fetching and serving mails to delicate programs,
    [[http://offlineimap.org/][offlineimap]] for fetching and updating mails and [[http://www.dovecot.org/][dovecot]] for serving
    mails.

    Since coordinating and properly configuring the three programs,
    /dovecot/, /offlineimap/ and /Gnus/, seems a little intricate at the
    beginning, I include all the settings to make the puzzle more
    complete.

    The whole workflow of this tool chain is as follows.

1. Offlineimap periodically updates mails in =Mail= folder,
2. Dovecot, the mail server, serves mail requests at local port (143
   for /imap/ and 993 for /imaps/), and
3. Gnus connects to the local ports, being listened by Dovecot.

*** Configuration

    All configurations to make Gnus work are included in this section.
    I would assume that all these programs have been correctly
    installed.

**** Dovecot

     This configuration is relatively simple.  Edit
     =DOVECOT_DIR/conf.d/10-mail.conf=, change the =mail_location=

     #+BEGIN_SRC conf
#mail_location = mbox:~/mail:INBOX=/var/mail/%u
mail_location = maildir:~/Mail:LAYOUT=fs
     #+END_SRC

     By default Dovecot uses Maildir++ directory layout which means that
     all mailboxes are stored in a single directory and prefixed with a
     dot.  But offlineimap by default works with =/= (forward slash),
     i.e., conventional hierarchical directories.  =LAYOUT\=fs= tells
     Dovecot to use hierarchical directories.

**** Offlineimap

     Offlineimap reads configuration from =~/.offlineimaprc=.  You can
     find a complete configuration sample, =offlineimap.conf=, shipped
     with installation.

     One thing to note is that Offlineimap /DOES NOT invoke itself/.  In
     order to periodically update mails, we need to either manually
     execute it or use other programs.  I use /cron/ utility to invoke
     Offlineimap every 5 minutes, as show in the following code.

     #+BEGIN_SRC conf
*/5 * * * * /usr/bin/offlineimap
     #+END_SRC

     My Offlineimap configuration is as follows.

     #+BEGIN_SRC conf
[general]

accounts = Tiger, Gmail, Ymail
maxsyncaccounts = 4

[Account Tiger]

localrepository = TigerLocal
remoterepository = TigerRemote

[Repository TigerLocal]

type = Maildir
localfolders = ~/Mail/Tiger
sep = /

[Repository TigerRemote]

type = IMAP
remotehost = outlook.office365.com

ssl = yes
sslcacertfile = /etc/ssl/certs/ca-certificates.crt

remoteport = 993
remoteuser = my_livemail_address
createfolders = False

[Account Gmail]

localrepository = GmailLocal
remoterepository = GmailRemote

[Repository GmailLocal]

type = Maildir
localfolders = ~/Mail/Gmail sep = /

[Repository GmailRemote]

type = Gmail
remoteuser = my_gmail_address
sslcacertfile = /etc/ssl/certs/ca-certificates.crt

[Account Ymail]

localrepository = YmailLocal
remoterepository = YmailRemote

[Repository YmailLocal]

type = Maildir
localfolders = ~/Mail/Ymail
sep = /

[Repository YmailRemote]

type = IMAP
remotehost = imap.mail.yahoo.com

ssl = yes
sslcacertfile = /etc/ssl/certs/ca-certificates.crt

remoteport = 993
remoteuser = my_ymail_address
createfolders = False
     #+END_SRC

**** Gnus

     Now comes the workhorse, /Gnus/.

     #+BEGIN_SRC emacs-lisp
(use-package gnus
  :bind ("<f12>" . gnus-other-frame)
  :config
  (setq gnus-init-file "/home/gongzhitaao/.emacs.d/gnus-conf.el"))
     #+END_SRC

** Orgmode
   :PROPERTIES:
:CUSTOM_ID: sec:orgmode
:END:

   #+BEGIN_SRC emacs-lisp
(use-package org
  :if (display-graphic-p)
  :config
  (let ((my-org-modules
         '(org-bbdb
           org-bibtex
           org-clock
           org-docview
           org-gnus
           org-habit
           org-table
           ox-latex
           ox-bibtex)))
    (dolist (m my-org-modules)
      (require m)))

  ;; Where I add todos.
  (add-hook 'org-mode-hook 'turn-on-reftex)
  (add-hook 'org-mode-hook 'turn-on-auto-fill)

  (define-key org-mode-map (kbd "C-c )") #'reftex-reference)
  (define-key org-mode-map (kbd "C-c [") #'reftex-citation)
  (define-key org-mode-map [remap fill-paragraph] #'org-fill-paragraph)
  (define-key org-mode-map (kbd "C-c C-\\")
    (lambda ()
      (interactive)
      (my-apply-region-or-para 'org-indent-region)))

  (setq org-directory (expand-file-name "org" my-personal-dir))

  (setq org-time-stamp-custom-formats
        '("<%m/%d/%y %a>" . "<%Y-%m-%d %a %R %z>"))

  ;; Recursive update todo statistics
  (setq org-hierarchical-todo-statistics nil)

  ;; Show events from diary
  (setq org-agenda-include-diary t)

  ;; Resolve open clocks if the user if idle more than 10 minutes.
  (setq org-clock-idle-time 10)

  ;; Sublevels inherit property from parents
  (setq org-use-property-inheritance t)

  ;; Fontify src blocks
  (setq org-src-fontify-natively t)
  (setq org-src-preserve-indentation t)

  ;; Press enter to follow links
  (setq org-return-follows-link t)

  ;; Use html5 and DO NOT include default styles and scripts.
  (setq org-html-doctype "html5"
        org-html-html5-fancy t
        org-html-head-include-default-style nil
        org-html-head-include-scripts nil)

  ;; Postamble.
  (setq org-html-postamble t org-html-postamble-format
        '(("en" "<a class=\"author\"
       href=\"http://gongzhitaao.org\">%a</a> / <span
       class=\"date\">%T</span><span class=\"creator\">%c</span>")))

  ;; Use prefix key as tag selection
  (setq org-use-fast-todo-selection t)

  ;; Bypassing logging if change state with Shift key
  (setq org-treat-S-cursor-todo-selection-as-state-change nil)

  (setq org-todo-keywords
        '((sequence
           "TODO(t)" "NEXT(n)" "|"
           "DONE(d!)")
          (sequence
           "WAIT(w@/!)" "HOLD(h@/!)" "|"
           "KILL(k@)")))

  (setq org-todo-keyword-faces
        '(("TODO" :foreground "red" :weight bold)
          ("NEXT" :foreground "cyan" :weight bold)
          ("DONE" :foreground "green" :weight bold)
          ("WAIT" :foreground "yellow" :weight bold)
          ("HOLD" :foreground "magenta" :weight bold)
          ("KILL" :foreground "forest green" :weight bold)))

  ;; Files to be included in Agenda view.
  (setq org-agenda-files
        (expand-file-name "orgfile" org-directory))


  (setq org-agenda-dim-blocked-tasks t)
  (setq org-agenda-compact-blocks t)

  (setq org-agenda-repeating-timestamp-show-all t)
  (setq org-agenda-show-all-dates t)

  (setq org-agenda-prefix-format
        '((agenda . " %i %-12:c%?-12t% s")
          (timeline . "  % s")
          (todo . " %i %-12:T")
          (tags . " %i %-12:T")
          (search . " %i %-12:T")))

  (setq org-agenda-tags-column -100
        org-habit-graph-column 45
        org-habit-preceding-days 28
        org-habit-following-days 1
        org-agenda-start-with-log-mode t)

  (setq org-clock-history-length 32
        org-clock-in-resume t)
  (setq org-log-into-drawer t
        org-clock-into-drawer t)

  (setq org-clock-persist 't)
  (org-clock-persistence-insinuate)

  (setq org-use-fast-tag-selection nil)

  (setq org-capture-templates
        '(("t" "New TODO" entry
           (file+headline "todo.org.gz" "Tasks")
           "* TODO %^{Title} %^G\n %u\n %?\n\n\n")
          ("p" "New Project Proposal" entry
           (file+headline "proj.org.gz" "Projects")
           "* %^{Title} %^G\n %u\n %?\n\n\n")))

  (setq org-latex-pdf-process
        (quote ("texi2dvi --pdf --clean --verbose --batch %f")))

  (add-to-list
   'org-latex-classes
   '("scrartcl"
     "\\documentclass{scrartcl} [NO-DEFAULT-PACKAGES] [NO-PACKAGES] [EXTRA]"
     ("\\section{%s}" . "\\section*{%s}")
     ("\\subsection{%s}" . "\\subsection*{%s}")
     ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
     ("\\paragraph{%s}" . "\\paragraph*{%s}")
     ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))

  (add-to-list
   'org-latex-classes
   '("scrreprt"
     "\\documentclass{scrreprt} [NO-DEFAULT-PACKAGES] [NO-PACKAGES] [EXTRA]"
     ("\\section{%s}" . "\\section*{%s}")
     ("\\subsection{%s}" . "\\subsection*{%s}")
     ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
     ("\\paragraph{%s}" . "\\paragraph*{%s}")
     ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))

  (setq org-publish-project-alist '(("emacsdotd"
                                     :base-directory "~/.emacs.d/"
                                     :base-extension "org"
                                     :publishing-directory "~/Documents/emacsdotd"
                                     :publishing-function org-html-publish-to-html
                                     :htmlized-source t))))
   #+END_SRC

** Miscellaneous Packages
   :PROPERTIES:
:CUSTOM_ID: sec:miscpac
:END:

   The followings are light yet serious functionalities.  Some of which
   that are wrapped in =(when (display-graphic-p) ...)= are intended to
   be loaded only in GUI mode.  When in console mode, e.g., editing
   files through SSH, I only need core editing functions.

*** Diminish

    Make minor modes invisible.  As quoted from Will Mengarini in
    [[http://www.eskimo.com/~seldon/diminish.el][diminish.el]],

    #+BEGIN_QUOTE
    When we diminish a mode, we are saying we want it to continue doing
    its work for us, but we no longer want to be reminded of it. It
    becomes a night worker, like a janitor; it becomes an invisible man;
    it remains a component, perhaps an important one, sometimes an
    indispensable one, of the mechanism that maintains the day-people's
    world, but its place in their thoughts is diminished, usually to
    nothing. As we grow old we diminish more and more such thoughts,
    such people, usually to nothing.
    #+END_QUOTE

    #+BEGIN_SRC emacs-lisp
(use-package diminish)
    #+END_SRC

*** Ag

    Search like crazy.  It is a code-searching tool alternative for ack
    related frontend, e.g., ack-and-a-half, which is not actively
    maintained anymore.  I included this package but never used before!!

    #+BEGIN_SRC emacs-lisp
(use-package ag
  :defines my-ag-keymap
  :bind-keymap ("C-c a" . my-ag-map)
  :config

  (setq ag-reuse-buffers t    ; Don't spam buffer list with ag buffers
        ag-highlight-search t ; A little fanciness

        ;; Use Projectile to find the project root
        ag-project-root-function
        (lambda (d)
          (let ((default-directory d))
            (projectile-project-root))))

  (defvar my-ag-map
    (let ((map (make-sparse-keymap)))
      (define-key map (kbd "a") #'ag-regexp)
      (define-key map (kbd "p") #'ag-project-regexp)

      map)))
    #+END_SRC

*** Anzu

    Display in the modeline search information, i.e, =(cur/total)=,
    where =cur= is the current index of searched keyword and total is
    number of totally matched keywords in the current buffer, as shown
    in Figure [[fig:anzu]].

    #+CAPTION: Anzu minor mode
    #+NAME: fig:anzu
    [[./img/anzu.png]]

    #+BEGIN_SRC emacs-lisp
(use-package anzu
  :init (global-anzu-mode +1)
  :diminish anzu-mode)
    #+END_SRC

*** Appt

    #+BEGIN_QUOTE
    The Emacs diary keeps track of appointments or other events on a
    daily basis, in conjunction with the calendar.
    #+END_QUOTE

    #+BEGIN_SRC emacs-lisp
(when (display-graphic-p)
  (setq diary-file (expand-file-name "diary" my-personal-dir)))
    #+END_SRC

    Show diary when I view the calendar.

    #+BEGIN_SRC emacs-lisp
(setq calendar-view-diary-initially-flag t)
    #+END_SRC

    Activate appointment management and remind Org agenda as appoinment,
    only in GUI mode.

    #+BEGIN_SRC emacs-lisp
(when (display-graphic-p)
  (appt-activate 1)
  (add-hook 'org-finalize-agenda-hook 'org-agenda-to-appt))
    #+END_SRC

    Display the coming appointment in a notification popup.

    #+BEGIN_SRC emacs-lisp
(when (display-graphic-p)
  (defun my-appt-display (mins-till-appt cur-time msg)
    "Convinient wrapper for appt popup display"
    (notifications-notify
     :title (format "Appt in %s minute(s)" mins-till-appt)
     :body msg
     :app-icon (expand-file-name "appointment-soon.png" my-icons-dir)))

  (setq appt-disp-window-function (function my-appt-display)))
    #+END_SRC

*** Async

    Async in Emacs?  Cool!

    #+BEGIN_SRC emacs-lisp
(use-package async
  :commands (dired-async-mode)
  :init (dired-async-mode 1))
    #+END_SRC

*** BBDB

    [[http://savannah.nongnu.org/projects/bbdb/][BBDB]] (Insidious Big Brother Database) is a rolodex-like database
    program for GNU Emacs.  It is mainly used to store contacts.  And it
    has nice integration with Gnus.

    #+BEGIN_SRC emacs-lisp
(use-package bbdb
  :if (display-graphic-p)
  :config
  (bbdb-initialize 'gnus 'mail 'message 'anniv)

  (setq bbdb-complete-mail-allow-cycling t
        bbdb-allow-duplicates t
        bbdb-message-all-addresses t
        bbdb-file
        (expand-file-name "contacts.bbdb.gz" my-personal-dir))

  (add-hook 'message-setup-hook 'bbdb-mail-aliases))
    #+END_SRC

*** Deft

    #+BEGIN_QUOTE
    [[http://jblevins.org/projects/deft/][Deft]] is an Emacs mode for quickly browsing, filtering, and editing
    directories of plain text notes, inspired by [[http://notational.net/][Notational Velocity]].
    #+END_QUOTE

    #+BEGIN_SRC emacs-lisp
(use-package deft
  :if (display-graphic-p)
  :bind ("<f8>" . deft)
  :config
  (setq deft-extension "org"
        deft-directory (expand-file-name "notes" my-personal-dir)
        deft-text-mode 'org-mode
        deft-use-filename-as-title nil
        deft-auto-save-interval 0
        deft-strip-title-regexp
        (concat deft-strip-title-regexp
                "\\|\\(?:\\+TITLE:[[:space:]]+\\)")))
    #+END_SRC

*** Dired

    It is a really cool bultin package of which I have not yet leveraged
    the full power.

    #+BEGIN_SRC emacs-lisp
(put 'dired-find-alternate-file 'disabled nil)

;; always delete and copy recursively
(setq dired-recursive-deletes 'always
      dired-recursive-copies 'always
      dired-listing-switches "-alh")

(use-package dired-x)
    #+END_SRC

*** Display-time

    Display time and unread mail notification, if not in terminal, in
    the mode line.

    #+BEGIN_SRC emacs-lisp
(setq display-time-24hr-format t display-time-day-and-date nil)

(when (display-graphic-p)

  (setq display-time-mail-function
        (lambda () ;; Gnus launched?
          (when (boundp 'gnus-newsrc-alist)
            (dolist (entry gnus-newsrc-alist)
              (let ((group (car entry)))
                (when (< (gnus-group-level group) 2)
                  (let ((unread (gnus-group-unread group)))
                    (when (and (numberp unread)
                               (> unread 0))
                      group))))))))

  (setq display-time-use-mail-icon t
        display-time-mail-icon
        `(image :type png
                :file ,(expand-file-name "mail-unread.png" my-icons-dir)
                :ascent center)))

(display-time)
    #+END_SRC

*** Eshell

    Emacs shell program.  Very handy for remote console access.

    #+BEGIN_SRC emacs-lisp
(use-package eshell
  :config
  ;; Truncate eshell buffer just in case you got megabytes of output
  (add-to-list 'eshell-output-filter-functions 'eshell-truncate-buffer)
  (setq eshell-directory-name (expand-file-name "eshell" my-tmp)))
    #+END_SRC

*** ESS

    #+BEGIN_QUOTE
    [[http://ess.r-project.org/][ESS]] (Emacs Speaks Statistics) is an add-on package for Emacs text
    editors such as GNU Emacs and XEmacs.  It is designed to support
    editing of scripts and interaction with various statistical analysis
    programs such as R, S-Plus, SAS, Stata and OpenBUGS/JAGS.
    #+END_QUOTE

    I think [[http://julialang.org/][Julia]] is also supported.

    #+BEGIN_SRC emacs-lisp
(use-package ess-site
  :defer t
  :config
  (add-hook 'ess-mode-hook
            (lambda ()
              (setq ess-help-own-frame 'one)
              (setq ess-indent-level 2)
              (setq ess-first-continued-statement-offset 2)
              (setq ess-continued-statement-offset 0)
              (setq ess-tab-complete-in-script t)
              (setq ess-first-tab-never-complete
                    'symbol-or-paren-or-punct)))

  (add-hook 'inferior-ess-mode-hook
            (lambda ()
              (smartparens-mode 1))))
    #+END_SRC

*** Expand-region

    Select the region in a DWIW style.

    #+BEGIN_SRC emacs-lisp
(use-package expand-region
  :bind ("C-=" . er/expand-region))
    #+END_SRC

*** Flycheck

    Eamcs Front-end for various languages syntax checker.

    #+BEGIN_SRC emacs-lisp
(use-package flycheck
  :if (display-graphic-p)
  :config
  (add-hook 'after-init-hook #'global-flycheck-mode)
  :diminish flycheck-mode)
    #+END_SRC

*** Javascript

    #+BEGIN_SRC emacs-lisp
(use-package js2-mode
  :mode "\\.js\\'"
  :config
  (setq js2-basic-offset 2
        js2-include-node-externs t
        js2-include-browser-externs t)

  (rename-modeline "js2-mode" js2-mode "JS2"))
    #+END_SRC

*** Helm

    #+BEGIN_SRC emacs-lisp
(defvar helm-command-prefix-key)
(setq helm-command-prefix-key nil)
(use-package helm-config
  :bind-keymap ("C-c h" . helm-command-map)
  :bind ("<f9>" . helm-recentf)
  :config
  ;; Some custom helm bindings
  (define-key helm-command-map (kbd "a") #'helm-apropos)
  (define-key helm-command-map (kbd "g") #'helm-do-grep)
  (define-key helm-command-map (kbd "o") #'helm-occur)
  (define-key helm-command-map (kbd "p") #'helm-projectile)

  (use-package helm-files)
  (setq helm-recentf-fuzzy-match t
        helm-buffers-fuzzy-matching t
        helm-completion-in-region-fuzzy-match t
        helm-mode-fuzzy-match t))
    #+END_SRC

*** Ibuffer

    #+BEGIN_SRC emacs-lisp
(use-package ibuffer
  :config

  (setq ibuffer-saved-filter-groups
        `(("default"
           ("Planner"
            (or (mode . org-agenda-mode)
                (filename . "/home/gongzhitaao/Dropbox/emacs/personal/org/")
                (name . "\\.bbdb")
                (mode . bbdb-mode)
                (name . "^\\*Calendar\\*$")
                (name . "^diary$")))
           ("Dired" (mode . dired-mode))
           ("Web"
            (or (name . "\\.js")
                (name . "\\.css")
                (name . "\\.html")
                (name . "\\.php")
                (name . "\\.xml")
                (mode . yaml-mode)))
           ("Text"
            (or (name . "\\.\\(tex\\|bib\\|csv\\)")
                (mode . org-mode)
                (mode . markdown-mode)
                (mode . text-mode)))
           ("Data"
            (or (mode . gnuplot-mode)
                (mode . octave-mode)
                (mode . R-mode)))
           ("Coding"
            (or (mode . shell-script-mode)
                (mode . sh-mode)
                (mode . emacs-lisp-mode)
                (name . "\\.[ch]\\(pp\\|xx\\|\\+\\+\\)?")
                (mode . python-mode)
                (name . "\\.ya?ml")
                (name . "\\.sql")))
           ("Mail"
            (or (mode . message-mode)
                (mode . mail-mode)
                (mode . gnus-group-mode)
                (mode . gnus-summary-mode)
                (mode . gnus-article-mode)
                (mode . gnus-server-mode)
                (mode . gnus-browse-mode)
                (name . "^\\.newsrc-dribble")))
           ("Console"
            (or (mode . inferior-ess-mode)
                (mode . inferior-python-mode)
                (mode . eshell-mode)
                (mode . gnuplot-comint-mode)
                (mode . comint-mode)))
           ("Helper"
            (or (mode . makefile-mode)
                (mode . makefile-gmake-mode)
                (mode . cmake-mode)
                (mode . calc-mode)
                (mode . Info-mode)
                (mode . help-mode)
                (mode . ess-help-mode)
                (name . "^\\*scratch\\*$"))))))

  (add-hook
   'ibuffer-mode-hook
   (lambda ()
     (ibuffer-auto-mode 1)
     (ibuffer-switch-to-saved-filter-groups "default")
     (local-set-key (kbd "<right>") 'ibuffer-forward-filter-group)
     (local-set-key (kbd "<left>") 'ibuffer-backward-filter-group)
     (substitute-key-definition
      'ibuffer-find-file 'ido-find-file (current-local-map))
     (hl-line-mode 1)))

  (define-ibuffer-column size-h
    (:name "Size" :inline t)
    (cond ((> (buffer-size) 1000)
           (format "%7.1fk" (/ (buffer-size) 1000.0)))
          ((> (buffer-size) 1000000)
           (format "%7.1fM" (/ (buffer-size) 1000000.0)))
          (t (format "%8dB" (buffer-size)))))

  (setq ibuffer-formats
        '((mark modified read-only " "
                (name 18 18 :left :elide) " "
                (size-h 9 -1 :right) " "
                (mode 16 16 :left :elide) " "
                filename-and-process))))
    #+END_SRC

*** Ido

    #+BEGIN_SRC emacs-lisp
(use-package ido
  :config
  (ido-mode 'both)

  (setq ido-save-directory-list-file
        (expand-file-name "idolast" my-tmp)
        ;; ignore these buffers during completion
        ido-ignore-buffers
        '("\\` " "^\*Mess" "^\*Back" ".*Completion" "^\*Ido" "^\*trace" "^\*compilation" "^\*GTAGS" "^session\.*" "^\*")
        ;; case insensitive
        ido-case-fold t
        ;; remember last directory
        ido-enable-last-directory-history t
        ido-max-work-file-list 50
        ido-use-filename-at-point nil
        ido-use-url-at-point nil
        ido-enable-flex-matching nil
        ido-max-prospects 6
        ido-confirm-unique-completion t)

  ;; increase minibuffer size when ido completion is active
  (add-hook 'ido-minibuffer-setup-hook
            (function
             (lambda ()
               (make-local-variable
                'resize-minibuffer-window-max-height)))))
    #+END_SRC
*** Midnight

    What is =midnight-mode= for?  Included but never used...

    #+BEGIN_SRC emacs-lisp
(use-package midnight)
    #+END_SRC

*** Multiple-cursors

    #+BEGIN_SRC emacs-lisp
(use-package multiple-cursors
  :defines my-multiple-cursors-map
  :bind-keymap ("C-c m" . my-multiple-cursors-map)
  :config
  (defvar my-multiple-cursors-map
    (let ((map (make-sparse-keymap)))
      (define-key map (kbd "l") #'mc/edit-lines)
      (define-key map (kbd "C-a") #'mc/edit-beginnings-of-lines)
      (define-key map (kbd "C-e") #'mc/edit-ends-of-lines)
      (define-key map (kbd "C-s") #'mc/mark-all-in-region)
      (define-key map (kbd "n") #'mc/mark-next-like-this)
      (define-key map (kbd "p") #'mc/mark-previous-like-this)
      (define-key map (kbd "e") #'mc/mark-more-like-this-extended)
      (define-key map (kbd "h") #'mc/mark-all-like-this-dwim)
      (define-key map (kbd "r") #'mc/mark-all-in-region-regexp)

      map)))
    #+END_SRC

*** Projectile

    #+BEGIN_SRC emacs-lisp
(use-package projectile
  :init
  (projectile-global-mode)
  :config
  (define-key projectile-mode-map [remap projectile-ack] #'projectile-ag)
  (setq projectile-completion-system 'grizzl)
  :diminish projectile-mode)
    #+END_SRC

*** Recentf

    Save recently opened files.

    #+BEGIN_SRC emacs-lisp
(use-package recentf
  :config
  (setq recentf-save-file (expand-file-name "recentf" my-tmp))
  (add-to-list 'recentf-exclude (expand-file-name ".*" my-tmp))
  (add-to-list 'recentf-exclude (expand-file-name "elpa/.*" my-dir))
  (add-to-list 'recentf-exclude (expand-file-name "~/.newsrc*"))
  (add-to-list 'recentf-exclude (expand-file-name my-personal-dir))
  (add-to-list 'recentf-exclude (expand-file-name ".cask/.*" my-dir))
  (recentf-mode +1))
    #+END_SRC

*** Savehist

    Save minibuffer history.

    #+BEGIN_SRC emacs-lisp
(use-package savehist
  :init
  (savehist-mode +1)
  :config
  (setq savehist-additional-variables '(search ring regexp-search-ring)
        savehist-file (expand-file-name "savehist" my-tmp)))
    #+END_SRC

*** Saveplace

    Save places in a file so that you can go back when you reopen it.

    #+BEGIN_SRC emacs-lisp
(use-package saveplace
  :init
  (setq-default save-place t)
  :config
  (setq save-place-file (expand-file-name "saveplace" my-tmp)))
    #+END_SRC

*** Smartparens

    #+BEGIN_QUOTE
    Smartparens is minor mode for Emacs that /deals with parens pairs
    and tries to be smart about it/.
    #+END_QUOTE

    This is a really /smart/ and /useful/ package.  /However it takes a
    while, maybe quite a while, to get used to its intelligence/.  For
    most editors (I really mean editors other than Emacs), I can not
    imagine I may have all these convenient options of dealing with
    parens.  Take as an simple example, kill the ballanced expression.

    #+BEGIN_SRC lisp :tangle no
(func1 (func2 (func3)))
    #+END_SRC

    Suppose you want to delete =(func2 ...)=, normally I would delete
    character by character, or hightlight manually and then delete.
    With /smartparens/, I may place cursor at the opening bracket of
    =func2= and {{{kbd(M-x)}}} =sp-kill-sexp= would kill the whole
    =func2= expression.

    #+BEGIN_SRC emacs-lisp
(use-package smartparens
  :init

  (smartparens-global-mode t)
  (show-smartparens-global-mode 1)

  :diminish smartparens-mode

  :config

  (sp-with-modes
      '(tex-mode plain-tex-mode latex-mode)
    (sp-local-tag "i" "\"<" "\">")
    (sp-local-tag "i" "\"[" "\"]"))

  (--each '("*" "/" "=" "~")
    (sp-local-pair 'org-mode it it))

  (sp-local-pair '(emacs-lisp-mode lisp-mode) "`" "'")
  (sp-local-pair '(emacs-lisp-mode lisp-mode) "`"
                 nil :when '(sp-in-string-p))
  (sp-local-pair '(emacs-lisp-mode lisp-mode) "'"
                 nil :actions nil)

  (setq sp-cancel-autoskip-on-backward-movement nil)
  (setq sp-navigate-consider-stringlike-sexp
        '(lisp-mode emacs-lisp-mode latex-mode LaTeX-mode TeX-mode))

  (define-key smartparens-mode-map (kbd "C-c s f") 'sp-forward-sexp)
  (define-key smartparens-mode-map (kbd "C-c s b") 'sp-backward-sexp)

  (define-key smartparens-mode-map (kbd "C-c s d") 'sp-down-sexp)
  (define-key smartparens-mode-map (kbd "C-c s D") 'sp-backward-down-sexp)
  (define-key smartparens-mode-map (kbd "C-c s a") 'sp-beginning-of-sexp)
  (define-key smartparens-mode-map (kbd "C-c s e") 'sp-end-of-sexp)

  (define-key smartparens-mode-map (kbd "C-c s u") 'sp-up-sexp)
  (define-key smartparens-mode-map (kbd "C-c s U") 'sp-backward-up-sexp)
  (define-key smartparens-mode-map (kbd "C-c s t") 'sp-transpose-sexp)

  (define-key smartparens-mode-map (kbd "C-c s n") 'sp-next-sexp)
  (define-key smartparens-mode-map (kbd "C-c s p") 'sp-previous-sexp)

  (define-key smartparens-mode-map (kbd "C-c s k") 'sp-kill-sexp)
  (define-key smartparens-mode-map (kbd "C-c s w") 'sp-copy-sexp)

  (define-key smartparens-mode-map (kbd "C-c s s") 'sp-forward-slurp-sexp)
  (define-key smartparens-mode-map (kbd "C-c s r") 'sp-forward-barf-sexp)
  (define-key smartparens-mode-map (kbd "C-c s S") 'sp-backward-slurp-sexp)
  (define-key smartparens-mode-map (kbd "C-c s R") 'sp-backward-barf-sexp)
  (define-key smartparens-mode-map (kbd "C-c s F") 'sp-forward-symbol)
  (define-key smartparens-mode-map (kbd "C-c s B") 'sp-backward-symbol)

  (define-key smartparens-mode-map (kbd "C-c s [") 'sp-select-previous-thing)
  (define-key smartparens-mode-map (kbd "C-c s ]") 'sp-select-next-thing)

  (define-key smartparens-mode-map (kbd "C-c s C-i") 'sp-splice-sexp)
  (define-key smartparens-mode-map (kbd "C-c s <delete>") 'sp-splice-sexp-killing-forward)
  (define-key smartparens-mode-map (kbd "C-c s <backspace>") 'sp-splice-sexp-killing-backward)
  (define-key smartparens-mode-map (kbd "C-c s C-<backspace>") 'sp-splice-sexp-killing-around)

  (define-key smartparens-mode-map (kbd "C-c s C-w") 'sp-wrap)
  (define-key smartparens-mode-map (kbd "C-c s C-u") 'sp-unwrap-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-b") 'sp-backward-unwrap-sexp)

  (define-key smartparens-mode-map (kbd "C-c s C-t") 'sp-prefix-tag-object)
  (define-key smartparens-mode-map (kbd "C-c s C-p") 'sp-prefix-pair-object)
  (define-key smartparens-mode-map (kbd "C-c s C-c") 'sp-convolute-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-a") 'sp-absorb-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-e") 'sp-emit-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-p") 'sp-add-to-previous-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-n") 'sp-add-to-next-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-j") 'sp-join-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-s") 'sp-split-sexp)
  (define-key smartparens-mode-map (kbd "C-c s C-r") 'sp-raise-sexp))
    #+END_SRC

*** Tex

    #+BEGIN_SRC emacs-lisp
(setq TeX-auto-save t)
(setq TeX-parse-self t)

(setq bibtex-dialect 'biblatex)
(setq bibtex-align-at-equal-sign t)
(setq bibtex-text-indentation 20)

(add-hook 'bibtex-mode-hook
          (lambda ()
            (local-set-key (kbd "C-c \\") 'bibtex-fill-entry)
            (setq fill-column 140)))

(use-package reftex
  :diminish reftex-mode
  :config
  (add-hook 'latex-mode-hook 'turn-on-reftex)
  (add-hook 'LaTeX-mode-hook 'turn-on-reftex)
  (setq reftex-plug-into-AUCTeX t
        reftex-ref-style-default-list '("Cleveref" "Hyperref" "Fancyref")
        reftex-default-bibliography
        '("/home/gongzhitaao/Dropbox/bib/nn.bib"
          "/home/gongzhitaao/Dropbox/bib/sp.bib")))
    #+END_SRC

*** TRAMP

    Use /TRAMP/ (Transparent Remote Access, Multiple Protocols) to edit
    remote files.

    #+BEGIN_SRC emacs-lisp
(use-package tramp
  :config
  (setq tramp-default-method "ssh"
        tramp-persistency-file-name
        (expand-file-name "tramp" my-tmp)))
    #+END_SRC

    Expand region increases the selected region by semantic units.  I
    included this package but never knew it before!!

*** Undo-tree

    Visualize the undo list in a tree-like structure for easy undo and
    redo.

    #+BEGIN_SRC emacs-lisp
(use-package undo-tree
  :init
  (global-undo-tree-mode +1)
  :bind ("C-c u" . undo-tree-visualize)
  :diminish undo-tree-mode)
    #+END_SRC

*** Uniquify

    Distinguish buffers with the same name.

    #+BEGIN_SRC emacs-lisp
(use-package uniquify
  :config
  (setq uniquify-buffer-name-style 'forward
        uniquify-separator "/"
        uniquify-after-kill-buffer-p t
        uniquify-ignore-buffers-re "^\\*"))
    #+END_SRC

*** Volatile-highlights

    This package highlights changes just made to the buffer and the
    highlights dispear at the next command.  It gives you a visual
    feedback what is being changed.

    #+BEGIN_SRC emacs-lisp
(use-package volatile-highlights
  :config
  (volatile-highlights-mode t)
  :diminish volatile-highlights-mode)
    #+END_SRC

*** Writeroom-mode

    #+BEGIN_SRC emacs-lisp
(use-package writeroom-mode
  :bind ("C-c w" . writeroom-mode)
  :config (setq writeroom-width (+ fill-column 10)))
    #+END_SRC

* Editor Setting
  :PROPERTIES:
:CUSTOM_ID: sec:editorsetting
:END:

  After the above preparations, we continue to customize the default
  behaviours of our editor.  First and formost, I would like to avoid
  accidentally closing Emacs.

  #+BEGIN_SRC emacs-lisp
(setq confirm-kill-emacs 'yes-or-no-p)
  #+END_SRC

  Then load the theme package and enable =Hl-mode=.  The face has to be
  set after loading the themes.

  #+BEGIN_SRC emacs-lisp
(load-theme 'naquadah t)
(global-hl-line-mode +1)
(set-face-background 'hl-line "#3B3D3A")
(set-face-foreground 'highlight nil)
  #+END_SRC

** Encoding and Font
   :PROPERTIES:
:CUSTOM_ID: sec:encoding_font
:END:

   First of all, it is the encoding system that matters.

   I will stick to =utf-8= whenever possible.  In case of Chinese,
   however, the default encoding under MS Windows is =cp936= (for
   Simplified Chinese) and =cp950= (for Big5), =gb18030= and =gb2312= in
   some cases.  I include them in the coding system in order to open
   these files correctly.  And note that =prefer-coding-system= always
   prefers the last preferred encoding, =utf-8= in the following code.

   #+BEGIN_SRC emacs-lisp
(let ((my-prefer-coding-system
       '(cp950 gb2312 cp936 gb18030 utf-16 utf-8)))
  (dolist (c my-prefer-coding-system)
    (prefer-coding-system c)))
   #+END_SRC

   Then comes the font for both English and Chinese.

   #+BEGIN_SRC emacs-lisp
(set-face-attribute 'default nil
                    :font "Ubuntu Mono:pixelsize=16")

(dolist (charset '(kana han symbol cjk-misc bopomofo))
  (set-fontset-font
   (frame-parameter nil 'font)
   charset (font-spec :family "WenQuanYi Zen Hei Mono"
                      :size 16)))
   #+END_SRC

** Default Behaviours
   :PROPERTIES:
:CUSTOM_ID: sec:default_behaviour
:END:

   Next is the tab thing.  Although I do not use =\t= for indentation, I
   still set the =tab-width= in case I need it, e.g., Makefile.

   #+BEGIN_SRC emacs-lisp
(setq-default indent-tabs-mode nil)
(setq-default tab-width 8)
(setq-default tab-stop-list (number-sequence 2 120 2))
   #+END_SRC

   Typing overwrites selected text.  Expected behaviour of most editors.

   #+BEGIN_SRC emacs-lisp
(delete-selection-mode t)
   #+END_SRC

   Leaving double space after =.= makes it easier to delete a whole line
   with {{{kbd(M-k)}}}.

   #+BEGIN_SRC emacs-lisp
(setq colon-double-space t)
   #+END_SRC

   Cleanup spaces and write timestamp if needed.

   #+BEGIN_SRC emacs-lisp
(add-hook 'before-save-hook
          (lambda ()
            (delete-trailing-whitespace)
            (time-stamp)))

(setq tab-always-indent 'complete)
(blink-cursor-mode 0)
(setq scroll-preserve-screen-position t)
(setq require-final-newline t)
(mouse-avoidance-mode 'animate)
(setq blink-matching-paren nil)
   #+END_SRC

   I do not need tool bar but I find menu bar helpful in case I forget
   what operations are available in a major mode.

   #+BEGIN_SRC emacs-lisp
(tool-bar-mode 0)
(menu-bar-mode 1)
   #+END_SRC

   I do not need the scroll bar either.

   #+BEGIN_SRC emacs-lisp
(scroll-bar-mode 0)
(setq scroll-margin 0
      scroll-preserve-screen-position 1)
   #+END_SRC

   Turn on =subword-mode= so that {{{kbd(C-right)}}} moves in step of a
   subword.

   #+BEGIN_SRC emacs-lisp
(global-subword-mode 1)
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
(setq frame-title-format
      '("emacs%@"
        (:eval (system-name)) ": "
        (:eval
         (if (buffer-file-name) (abbreviate-file-name (buffer-file-name))
           "%b")) " [%*]"))

(setq visible-bell t)

(setq inhibit-startup-message t
      resize-mini-windows t)

(column-number-mode 1)
(setq size-indication-mode t)

(fset 'yes-or-no-p 'y-or-n-p)

(file-name-shadow-mode t)

(put 'narrow-to-region 'disabled nil)
(put 'narrow-to-page 'disabled nil)
(put 'narrow-to-defun 'disabled nil)
(put 'upcase-region 'disabled nil)
(put 'downcase-region 'disabled nil)
   #+END_SRC

   Many of the settings above are rather self-evident.  The following
   /advice/ is for {{{kbd(M-w)}}}, copy command in Emacs.  By default,
   {{{kbd(M-w)}}} operates on a selected region and does nothing when no
   text is selected.  Instead of doing nothing, we /advice/ it to copy
   current line. i.e., where cursor resides, when no region is active.

   #+BEGIN_SRC emacs-lisp
(defadvice kill-ring-save
    (before slick-copy activate compile)
  "When called interactively with no active region, copy a single
         line instead."
  (interactive
   (if mark-active
       (list (region-beginning)
             (region-end))
     (message "Copied line")
     (list (line-beginning-position)
           (line-beginning-position 2)))))
   #+END_SRC

   Backup files in the temp directory instead of clustering everywhere
   with tild-ended files.

   #+BEGIN_SRC emacs-lisp
(setq backup-directory-alist `((".*" . ,my-tmp)))
(setq auto-save-list-file-prefix
      (expand-file-name ".saves-" my-tmp))

(setq backup-by-copying t
      delete-old-versions t
      kept-new-versions 6
      kept-old-versions 2
      version-control t)
   #+END_SRC

   Open read-only files in =view-mode= minor mode.

   #+BEGIN_SRC emacs-lisp
(setq view-read-only t)
   #+END_SRC

   I do not show line numbers at the margin as I do not care.  But I do
   care when I want to jump to a certain line in the buffer.  So show me
   the line numbers only when I'm about to jump to a line.

   #+BEGIN_SRC emacs-lisp
(defun goto-line-with-feedback ()
  "Show line numbers temporarily, while prompting for the line
         number input"
  (interactive)
  (unwind-protect
      (progn
        (linum-mode 1)
        (goto-line (read-number "Goto line: ")))
    (linum-mode -1)))

(global-set-key [remap goto-line] 'goto-line-with-feedback)
   #+END_SRC

   Turn on =auto-fill-mode= by default.  For /historical/ (unknown)
   reasons, =auto-fill-mode= is named by "auto-fill-function".

   #+BEGIN_SRC emacs-lisp
(diminish 'auto-fill-function)
   #+END_SRC

** Key bindings
   :PROPERTIES:
:CUSTOM_ID: sec:key
:END:

*** Function Key Bindings

    Keybindings for {{{kbd(Fn)}}} keys.

    #+BEGIN_SRC emacs-lisp
(global-set-key (kbd "<f6>") 'calendar)
(global-set-key (kbd "<f7>") 'compile)
(global-set-key (kbd "<f8>") 'deft)
;; f9 -- helm-recentf
;; f10 -- menu
(global-set-key (kbd "<f11>") 'ispell)
;; f12 -- gnus-other-frame)
    #+END_SRC

*** Improved Standard Bindings

    #+BEGIN_SRC emacs-lisp
(global-set-key [remap execute-extended-command] #'smex)
(global-set-key [remap list-buffers] #'ibuffer)
(global-set-key [remap isearch-forward] #'isearch-forward-regexp)
(global-set-key [remap isearch-backward] #'isearch-query-replace-regexp)
    #+END_SRC

*** User Key Bindings

    User key bindings usually begin with {{{kbd(C-c)}}}.

    #+BEGIN_SRC emacs-lisp
;; C-c a -- my-ag-map
(global-set-key (kbd "C-c g") #'ace-jump-mode)
(global-set-key (kbd "C-c k") #'browse-kill-ring)
;; C-c m -- my-multiple-cursor-map
(global-set-key (kbd "C-c q") #'auto-fill-mode)
(global-set-key (kbd "C-c r") #'isearch-query-replace-regexp)
(global-set-key (kbd "C-c o a") #'org-agenda)
(global-set-key (kbd "C-c o c") #'org-capture)
;; C-c w -- writeroom-mode
(global-set-key (kbd "C-c ,") #'color-identifiers-mode)

(global-set-key (kbd "C-c C-=") #'align-regexp)
(global-set-key (kbd "C-c C-/")
                (function
                 (lambda ()
                   (interactive)
                   (my-apply-region-or-line
                    'comment-or-uncomment-region))))
(global-set-key (kbd "C-c C-\\")
                (function
                 (lambda ()
                   (interactive)
                   (my-apply-region-or-para
                    'indent-region))))

(global-set-key (kbd "C-c <left>") #'decrease-left-margin)
(global-set-key (kbd "C-c <right>") #'increase-left-margin)
(global-set-key (kbd "C-c C-<left>") #'decrease-left-margin)
(global-set-key (kbd "C-c C-<right>") #'increase-left-margin)
    #+END_SRC

*** Key logger

    Sometimes I want to analyze my Emacs key press frequency.  The
    builtin function =open-dribble-file= does exactly what I want.  But
    be aware that it logs /everything/, literally, /everything/
    including your passwords.  So you may remove this section from your
    configuration files.

    #+BEGIN_SRC emacs-lisp
(open-dribble-file
 (expand-file-name
  (format-time-string "~/.emacs.d/.keylog/key-%FT%H%M%S.log")))
    #+END_SRC

* End

  After all these things, start the server.

  #+BEGIN_SRC emacs-lisp
(add-hook 'after-init-hook 'server-start t)
  #+END_SRC

-----

* Footnotes

[fn:1] As a side note, I guess his blog style might be adopted from
  [[http://doc.norang.ca/][Bernt Hansen]] blog site.  Correct me if I'm wrong.
